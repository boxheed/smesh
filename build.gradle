plugins {
  id 'org.springframework.boot' version '2.6.3' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
  id 'pl.allegro.tech.build.axion-release' version '1.13.6'
  id "com.github.ben-manes.versions" version "0.42.0"
  id "com.fizzpod.info" version "11.2.1" apply false
  //id "com.jfrog.bintray" version "1.8.5" apply false
}

scmVersion {
  tag {
    prefix = 'release-'
  }
}

allprojects {
  repositories {
    mavenCentral()
  }

  group = 'com.fizzpod'
  project.version = scmVersion.version
}

subprojects {
  if (it.name =~ /.*server/ || it.name =~ /.*lib/) {
    
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'com.fizzpod.info'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'


    sourceCompatibility = '11'

    dependencyManagement {
      imports {
        mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:2021.0.0'
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:2.6.3'
      }
      dependencies {
        dependency 'joda-time:joda-time:2.10.13'
        dependency 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.13.1'
        dependency 'com.google.guava:guava:31.0.1-jre'
        dependency 'com.jayway.jsonpath:json-path:2.7.0'
        dependency 'javax.annotation:javax.annotation-api:1.3.2'
        dependencySet(group:'com.hazelcast', version: '4.0') {
          entry 'hazelcast'
          entry 'hazelcast-spring'
        }
        dependency 'junit:junit:4.13.2'
        dependency 'org.mockito:mockito-all:1.10.19'
        dependencySet(group:'com.github.jknack', version: '4.3.0') {
          entry 'handlebars'
          entry 'handlebars-helpers'
          entry 'handlebars-jackson2'
        }
        dependency 'org.apache.commons:commons-lang3:3.12.0'
        dependency 'commons-io:commons-io:2.11.0'
      }
    }

    //Exclude the log4j dependencies as will use logback
    configurations {
      all*.exclude group: 'log4j', module: 'log4j'
      all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      all*.exclude module: "spring-boot-starter-tomcat"
    }

    //Configure Jacoco
    jacoco {
      toolVersion = "0.8.5"
    }

    jacocoTestReport {
      reports {
        xml.enabled true
        csv.enabled false
      }
    }

    build.dependsOn jacocoTestReport

    //configure publishing 
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }


    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    project.tasks.findAll {
      it.name.startsWith("publish")
    }.each {
      it.dependsOn assemble
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }



  }

  //Apply the spring boot servlet app 
  if (it.name =~ /.*server/) {
    apply plugin: 'org.springframework.boot'

    dependencies {
      implementation 'org.springframework.boot:spring-boot-starter-undertow'
    }

    bootJar {
      launchScript()
    }

    publishing {
      publications {
        bootJava(MavenPublication) {
          artifact bootJar
          artifactId bootJar.baseName
        }
      }
    }


  }
}

