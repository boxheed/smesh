plugins {
  id 'org.springframework.boot' version '2.2.5.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
  id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}

scmVersion {
  tag {
    prefix = 'release-'
  }
}

allprojects {
  repositories {
    mavenCentral()
  }

  group = 'com.fizzpod'
  project.version = scmVersion.version
}

subprojects {
  if (name =~ /.*server/ || it.name =~ /.*lib/) {
    
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    sourceCompatibility = '11'

    dependencyManagement {
      imports {
        mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:Hoxton.SR3'
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:2.2.5.RELEASE'
      }
    }

    //Exclude the log4j dependencies as will use logback
    configurations {
      all*.exclude group: 'log4j', module: 'log4j'
      all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      all*.exclude module: "spring-boot-starter-tomcat"
    }
  }

  //Apply the spring boot servlet app 
  //TODO remove the 'lib'
  if (name =~ /.*server/) {
    apply plugin: 'org.springframework.boot'

    dependencies {
      implementation 'org.springframework.boot:spring-boot-starter-undertow'
    }
  }
}

